plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.3"
}

ext {
    ver = '1.0.3'
    pkg = 'com.austinv11.servicer'
}

group pkg
version ver

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError false
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'Servicer'
    }

    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            pom {
                name = 'Servicer'
                description = 'Easily wire Java services for loading via java.util.ServiceLoader'
                url = 'https://github.com/austinv11/Servicer'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'austinv11'
                        name = 'Austin Varela'
                        email = 'austinv11@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/austinv11/Servicer.git'
                    url = 'https://github.com/austinv11/Servicer'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId pkg
            artifactId 'Servicer'
            version ver
        }
    }
}
